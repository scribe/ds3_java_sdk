buildscript {
    ext.kotlin_version = '1.1.2-5'

    repositories {
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }

    dependencies {
        classpath "gradle.plugin.net.ossindex:ossindex-gradle-plugin:0.1.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.1"
    }
}

allprojects {
    group = 'com.spectralogic.escapepod'
    version = '0.1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'net.ossindex.audit'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        maven {
            url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1'
        }
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile 'io.reactivex.rxjava2:rxjava:2.1.0'
        compile 'io.reactivex.rxjava2:rxkotlin:2.0.0'
        compile 'com.google.guava:guava:21.0'
        compile "com.google.inject:guice:$guiceVersion"
        compile "com.google.inject.extensions:guice-assistedinject:$guiceVersion"
        compile group: 'org.slf4j', name: 'slf4j-api', version: "$slf4jVersion"
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.14.1'
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-nio:0.14.1'
        compile 'com.google.protobuf:protobuf-java:3.2.0'

        testCompile group: 'org.slf4j', name: 'slf4j-simple', version: "$slf4jVersion"
        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-core:1.3'
        testCompile 'org.mockito:mockito-core:1.+'
        testCompile 'org.assertj:assertj-core:3.6.2'
    }

    protobuf {
        generatedFilesBaseDir = "$projectDir/src"

        protoc {
            artifact = 'com.google.protobuf:protoc:3.2.0'
        }

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java { }
                }
            }
        }
    }

    compileKotlin.dependsOn 'generateProto'
    compileTestKotlin.dependsOn 'generateTestProto'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

project (":api") {
    dependencies {
        compile project(":util")
    }
}
project(":localcache") {
    dependencies {
        compile project(":api")
    }
}

project(":planner") {
    dependencies {
        compile project(":api")
    }
}

project(":mongopersistence") {
    dependencies {
        compile project(":api")
    }
}

project(":hazelcast") {
    dependencies {
        compile project(":api")
    }
}

project(":migrate") {
    dependencies {
        compile project(":api")
        compile project(":divaclient")
        compile project(":avidmamclient")
    }
}

project(":divaclient") {
    dependencies {
        compile project(":api")
        compile project(":restclientutils")
    }
}

project(":flashnetclient") {
    dependencies {
        compile project(":api")
        compile project(":restclientutils")
    }
}

project(":avidmamclient") {
    dependencies {
        compile project(":api")
        compile project(":restclientutils")
    }
}

project(":restclientutils") {
    dependencies {
        compile project(":util")
    }
}

project(":server") {
    dependencies {
        compile project(":api")
        compile project(":hazelcast")
        compile project(":localcache")
        compile project(":scheduler")
        compile project(":mongopersistence")
    }
}

project(":scheduler") {
    dependencies {
        compile project(":api")
    }
}
